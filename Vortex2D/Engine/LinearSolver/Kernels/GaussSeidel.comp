#version 450
#extension GL_ARB_separate_shader_objects : enable

layout (local_size_x = 16, local_size_y = 16) in;

layout(push_constant) uniform Consts
{
  int width;
  int height;
  float w;
  int red;
}consts;

layout(std430, binding = 0) buffer Pressure
{
  float value[];
}pressure;

struct Data
{
  vec4 weights;
  float diagonal;
  float div;
  int red;
};

layout(std430, binding = 1) buffer EquationData
{
  Data data[];
}equation;

void main()
{
  ivec2 pos = ivec2(gl_GlobalInvocationID);

  if (pos.x > 0 && pos.y > 0 && pos.x < consts.width - 1 && pos.y < consts.height - 1)
  {
    int index = pos.x + pos.y * consts.width;
    if ((pos.x + pos.y) % 2 == consts.red)
    {
      float oldPressure = pressure.value[index];
      Data data = equation.data[index];

      vec4 p;
      p.x = pressure.value[index + 1];
      p.y = pressure.value[index - 1];
      p.z = pressure.value[index + consts.width];
      p.w = pressure.value[index - consts.width];

      if (data.diagonal != 0.0)
      {
        pressure.value[index] = mix(oldPressure, (data.div - dot(p, data.weights)) / data.diagonal, consts.w);
      }
      else
      {
        pressure.value[index] = 0.0;
      }
    }
  }
}
