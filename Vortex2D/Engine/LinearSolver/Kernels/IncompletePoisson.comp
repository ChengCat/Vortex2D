#version 450
#extension GL_ARB_separate_shader_objects : enable

layout (local_size_x_id = 1, local_size_y_id = 2) in;

layout(push_constant) uniform Consts
{
  int width;
  int height;
}consts;

struct A
{
  vec4 weights;
  float diagonal;
};

layout(std430, binding = 0) buffer Matrix
{
  A data[];
}matrix;

layout(std430, binding = 1) buffer Input1
{
  float value[];
}pressure;

layout(std430, binding = 2) buffer Input2
{
  float value[];
}z;

const float w = 1.1;

void main()
{
    ivec2 pos = ivec2(gl_GlobalInvocationID);

    if (pos.x > 0 && pos.y > 0 && pos.x < consts.width - 1 && pos.y < consts.height - 1)
    {
        int index = pos.x + pos.y * consts.width;

        vec4 weights = matrix.data[index].weights;

        vec4 diagonalWeights;
        diagonalWeights.x = -matrix.data[index + 1].diagonal;
        diagonalWeights.y = -matrix.data[index - 1].diagonal;
        diagonalWeights.z = -matrix.data[index + consts.width].diagonal;
        diagonalWeights.w = -matrix.data[index - consts.width].diagonal;

        weights.x = diagonalWeights.x == 0.0 ? 0.0 : weights.x / diagonalWeights.x;
        weights.y = diagonalWeights.y == 0.0 ? 0.0 : weights.y / diagonalWeights.y;
        weights.z = diagonalWeights.z == 0.0 ? 0.0 : weights.z / diagonalWeights.z;
        weights.w = diagonalWeights.w == 0.0 ? 0.0 : weights.w / diagonalWeights.w;

        float diagonal = 1.0 + dot(weights.yw, weights.yw);

        float oldPressure = pressure.value[index];

        vec4 p;
        p.x = pressure.value[index + 1];
        p.y = pressure.value[index - 1];
        p.z = pressure.value[index + consts.width];
        p.w = pressure.value[index - consts.width];

        float centreDiagonal = matrix.data[index].diagonal;
        if (centreDiagonal != 0.0)
        {
          z.value[index] = (2.0 - w) * w * (oldPressure * diagonal + dot(p, weights)) / centreDiagonal;
        }
    }
}
