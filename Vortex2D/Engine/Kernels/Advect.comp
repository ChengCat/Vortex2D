#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable

layout (local_size_x_id = 1, local_size_y_id = 2) in;

layout(push_constant) uniform Consts
{
  int width;
  int height;
  float delta;
}consts;

layout(binding = 0, rgba32f) uniform image2D Velocity;
layout(binding = 1, rgba8) uniform image2D Field;
layout(binding = 2, rgba8) uniform image2D OutField;

#include "CommonAdvect.comp"

vec4 interpolate(vec2 xy)
{
   ivec2 ij = ivec2(floor(xy));
   vec2 f = xy - ij;

   return mix(mix(imageLoad(Field, ij + ivec2(0, 0)), imageLoad(Field, ij + ivec2(1, 0)), f.x),
              mix(imageLoad(Field, ij + ivec2(0, 1)), imageLoad(Field, ij + ivec2(1, 1)), f.x),
              f.y);
}

void main(void)
{
    uvec2 localSize = gl_WorkGroupSize.xy; // Hack for Mali-GPU

    ivec2 pos = ivec2(gl_GlobalInvocationID);
    if (pos.x < consts.width && pos.y < consts.height)
    {
        vec4 value = interpolate(trace_rk3(pos, consts.delta));
        imageStore(OutField, pos, value);
    }
}
