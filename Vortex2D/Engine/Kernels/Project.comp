#version 450
#extension GL_ARB_separate_shader_objects : enable

layout (local_size_x_id = 1, local_size_y_id = 2) in;

layout(push_constant) uniform Consts
{
  int width;
  int height;
  float delta;
}consts;

layout(std430, binding = 0) buffer Pressure
{
  float value[];
}pressure;

layout(binding = 1, r32f) uniform image2D FluidLevelSet;
layout(binding = 2, r32f) uniform image2D SolidLevelSet;
layout(binding = 3, rg32f) uniform image2D Velocity;

layout(std430, binding = 4) buffer Valid
{
  ivec2 value[];
}valid;

#include "CommonProject.comp"

void main()
{
  ivec2 pos = ivec2(gl_GlobalInvocationID);
  if (pos.x > 0 && pos.y > 0 && pos.x < consts.width && pos.y < consts.height)
  {
    vec2 cell = imageLoad(Velocity, pos).xy;

    float p = pressure.value[pos.x + pos.y * consts.width];
    float pxn = pressure.value[(pos.x - 1) + pos.y * consts.width];
    float pyn = pressure.value[pos.x + (pos.y - 1) * consts.width];

    vec2 pGrad = vec2(p-pxn, p-pyn);

    float phi = imageLoad(FluidLevelSet, pos).x;
    float phixn = imageLoad(FluidLevelSet, pos + ivec2(-1,0)).x;
    float phiyn = imageLoad(FluidLevelSet, pos + ivec2(0,-1)).x;

    vec2 theta = vec2(fraction_inside(phixn, phi), fraction_inside(phiyn, phi));
    pGrad /= max(theta, 0.01);

    vec2 mask = vec2(0.0);

    vec2 wuv = get_weight(pos);
    if (wuv.x > 0.0 && (phi < 0.0 || phixn < 0.0))
    {
      mask.x = 1.0;
      valid.value[pos.x + pos.y * consts.width].x = 1;
    }
    if (wuv.y > 0.0 && (phi < 0.0 || phiyn < 0.0))
    {
      mask.y = 1.0;
      valid.value[pos.x + pos.y * consts.width].y = 1;
    }

    vec2 new_cell = cell - consts.delta * pGrad * consts.width;
    imageStore(Velocity, pos, vec4(mask * new_cell, 0.0, 0.0));
  }
}
