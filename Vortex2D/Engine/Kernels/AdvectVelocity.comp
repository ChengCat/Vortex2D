#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable

layout(local_size_x_id = 1, local_size_y_id = 2) in;
layout(constant_id = 3) const int interpolationMode = 0;

layout(push_constant) uniform Consts
{
  int width;
  int height;
  float delta;
}
consts;

layout(binding = 0, rgba32f) uniform image2D Velocity;
layout(binding = 1, rgba32f) uniform image2D OutVelocity;

#include "CommonAdvect.comp"

void main(void)
{
  uvec2 localSize = gl_WorkGroupSize.xy;  // Hack for Mali-GPU

  vec2 pos = vec2(gl_GlobalInvocationID);
  if (pos.x < consts.width && pos.y < consts.height)
  {
    vec2 value;
    vec2 uvel = (pos + vec2(0.0, 0.5));
    vec2 vvel = (pos + vec2(0.5, 0.0));

    // u
    vec2 upos = uvel + velocity_trace_rk3(uvel / consts.width, consts.delta) * consts.width;
    value.x = get_velocity(upos).x;

    // v
    vec2 vpos = vvel + velocity_trace_rk3(vvel / consts.width, consts.delta) * consts.width;
    value.y = get_velocity(vpos).y;

    // store result
    imageStore(OutVelocity, ivec2(pos), vec4(value, 0.0, 0.0));
  }
}
